apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {

    compileSdkVersion config['compile-sdk']
    buildToolsVersion config['build-tools']

    defaultConfig {
        applicationId 'io.noties.markwon.app'
        minSdkVersion 23
        targetSdkVersion config['target-sdk']
        versionCode 1
        versionName version

        resConfig 'en'
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize '5g'
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java.srcDirs += '../sample-utils/annotations'
        }
    }
}

kapt {
    arguments {
        arg('markwon.samples.file', "${projectDir}/samples.json".toString())
    }
}

androidExtensions {
    features = ["parcelize"]
}

dependencies {
    kapt project(':sample-utils:processor')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation project(':markwon-core')

    deps.with {
        implementation it['x-recycler-view']
        implementation it['x-cardview']
        implementation it['x-fragment']
        implementation it['gson']
        implementation it['adapt']
        implementation it['debug']
    }
}
